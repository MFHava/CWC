
#          Copyright Michael Florian Hava 2012.
# Distributed under the Boost Software License, Version 1.0.
#    (See accompanying file LICENSE_1_0.txt or copy at
#          http://www.boost.org/LICENSE_1_0.txt)

CMAKE_MINIMUM_REQUIRED(VERSION 3.2.2)
PROJECT(CWC CXX)

OPTION(BUILD_TESTS "Build tests" OFF)
OPTION(BUILD_SAMPLES "Build samples" OFF)
OPTION(BUILD_DOCUMENTATION "Build documentation" OFF)

OPTION(USE_BOOST_REGEX "Enable fallback to Boost.Regex if Std.Regex is not available" OFF)

INCLUDE_DIRECTORIES(inc)

SET(CMAKE_ARCHIVE_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/lib/static)
SET(CMAKE_LIBRARY_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/lib)
SET(CMAKE_RUNTIME_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/bin)

SET(CWC_VERSION_MAJOR 0)
SET(CWC_VERSION_MINOR 9)
SET(CWC_VERSION_PATCH 2)
SET(CWC_LIB_DEBUG_POSTFIX "-debug")

SET(CWC_API_LIB_NAME cwc-${CWC_VERSION_MAJOR}.${CWC_VERSION_MINOR}.${CWC_VERSION_PATCH}-api)
SET(CWC_CTX_LIB_NAME cwc-${CWC_VERSION_MAJOR}.${CWC_VERSION_MINOR}.${CWC_VERSION_PATCH}-ctx)

IF(${MSVC})
	IF(${MSVC_VERSION} LESS 1800)
		MESSAGE(FATAL_ERROR "Unsupported MSVC Version")
	ELSE()
		IF(${MSVC_VERSION} GREATER 1900)
			MESSAGE(WARNING "Untested MSVC Version")
		ENDIF()
		SET(CWC_API_LIB_NAME ${CWC_API_LIB_NAME}-msvc${MSVC_VERSION})
		SET(CWC_CTX_LIB_NAME ${CWC_CTX_LIB_NAME}-msvc${MSVC_VERSION})
	ENDIF()
ENDIF()

SET(MAX_EXCEPTION_MESSAGE_LENGTH 256 CACHE STRING "Control maximum length of exception messages across DLL boundaries")

#BEGIN API-library
CONFIGURE_FILE(${CMAKE_CURRENT_SOURCE_DIR}/src/api/cwc.hpp.in ${CMAKE_CURRENT_SOURCE_DIR}/inc/cwc/cwc.hpp @ONLY)
FILE(GLOB_RECURSE CWC_API_SOURCES "src/api/*")
FILE(GLOB_RECURSE CWC_PUBLIC_HEADERS "inc/cwc/public/*")
ADD_LIBRARY(${CWC_API_LIB_NAME} STATIC "inc/cwc/cwc.hpp" ${CWC_PUBLIC_HEADERS} ${CWC_API_SOURCES})
SET_TARGET_PROPERTIES(${CWC_API_LIB_NAME} PROPERTIES
	DEBUG_POSTFIX ${CWC_LIB_DEBUG_POSTFIX}
	CXX_STANDARD 11
	CXX_STANDARD_REQUIRED ON
	COMPILE_DEFINITIONS CWC_BUILDING
)
#END API-library


#BEGIN CTX-library
CONFIGURE_FILE(${CMAKE_CURRENT_SOURCE_DIR}/src/ctx/context.cpp.in ${CMAKE_CURRENT_BINARY_DIR}/src/ctx/context.cpp @ONLY)
FILE(GLOB_RECURSE CWC_CTX_SOURCES "src/ctx/*")
FILE(GLOB_RECURSE CWC_INTERNAL_HEADERS "inc/cwc/internal/*")
FILE(GLOB_RECURSE CWC_CTX_GENERATED_SOURCES ${CMAKE_CURRENT_BINARY_DIR}/src/ctx/*)
ADD_LIBRARY(${CWC_CTX_LIB_NAME} STATIC ${CWC_INTERNAL_HEADERS} ${CWC_CTX_SOURCES} ${CWC_CTX_GENERATED_SOURCES})
SET_TARGET_PROPERTIES(${CWC_CTX_LIB_NAME} PROPERTIES
	DEBUG_POSTFIX  ${CWC_LIB_DEBUG_POSTFIX}
	CXX_STANDARD 11
	CXX_STANDARD_REQUIRED ON
	COMPILE_DEFINITIONS "CWC_BUILDING;CWC_HOST"
)
#END CTX-library



#BEGIN CWCC-application + test
#Find required libs for Boost fallback
SET(REQUIRED_BOOST_LIBS filesystem system)
IF(USE_BOOST_REGEX)
	TARGET_COMPILE_DEFINITIONS(${CWC_CTX_LIB_NAME} PRIVATE CWC_USE_BOOST_REGEX)
	SET(REQUIRED_BOOST_LIBS ${REQUIRED_BOOST_LIBS} regex)
ENDIF()
IF(BUILD_TESTS)
	SET(REQUIRED_BOOST_LIBS ${REQUIRED_BOOST_LIBS} unit_test_framework)
ENDIF()
IF(REQUIRED_BOOST_LIBS)
#TODO: add option to let user select kind of boost libs?!
	SET(Boost_USE_STATIC_LIBS ON)
	SET(Boost_USE_MULTITHREADED ON)
	FIND_PACKAGE(Boost 1.58.0 REQUIRED ${REQUIRED_BOOST_LIBS})
ENDIF()

SET(CWC_HOST_LIBS ${CWC_CTX_LIB_NAME} ${CWC_API_LIB_NAME} ${Boost_REGEX_LIBRARY})

CONFIGURE_FILE(${CMAKE_CURRENT_SOURCE_DIR}/src/cwcc/disclaimer.cpp.in ${CMAKE_CURRENT_BINARY_DIR}/src/cwcc/disclaimer.cpp @ONLY)
FILE(GLOB_RECURSE CWCC_SOURCES "src/cwcc/*")
FILE(GLOB_RECURSE CWCC_GENERATED_SOURCES ${CMAKE_CURRENT_BINARY_DIR}/src/cwcc/*)
ADD_EXECUTABLE(cwcc ${CWCC_SOURCES} ${CWCC_GENERATED_SOURCES})
TARGET_LINK_LIBRARIES(cwcc ${CWC_HOST_LIBS} ${Boost_FILESYSTEM_LIBRARY} ${Boost_SYSTEM_LIBRARY})
SET_SOURCE_FILES_PROPERTIES(src/cwcc/parser.cpp PROPERTIES
	COMPILE_FLAGS "-O3"#NOTE: this only works as VC++ ignores unknown flags; this flag is necessary because otherwise AR from BINUTILS can't handle the this file (gets very big in debug version)
)
SET_TARGET_PROPERTIES(cwcc PROPERTIES
	CXX_STANDARD 11
	CXX_STANDARD_REQUIRED ON
	COMPILE_DEFINITIONS CWC_HOST
)
IF(BUILD_TESTS)
	FILE(GLOB_RECURSE CWCC_TEST_SOURCES "test/cwcc/*")
	ADD_EXECUTABLE(cwcc-test ${CWCC_TEST_SOURCES})
	SET_SOURCE_FILES_PROPERTIES(test/cwcc/shared.cpp PROPERTIES
		COMPILE_FLAGS "-O3"#NOTE: this only works as VC++ ignores unknown flags; this flag is necessary because otherwise AR from BINUTILS can't handle the this file (gets very big in debug version)
	)
	SET_TARGET_PROPERTIES(cwcc-test PROPERTIES
		CXX_STANDARD 11
		CXX_STANDARD_REQUIRED ON
		COMPILE_DEFINITIONS CWC_HOST
	)
	TARGET_LINK_LIBRARIES(cwcc-test ${CWC_HOST_LIBS} ${Boost_UNIT_TEST_FRAMEWORK_LIBRARY})
ENDIF()
#END CWCC-application + test



#BEGIN documentation
IF(BUILD_DOCUMENTATION)
	FIND_PACKAGE(Doxygen REQUIRED)
	CONFIGURE_FILE(${CMAKE_CURRENT_SOURCE_DIR}/Doxyfile.in ${CMAKE_CURRENT_BINARY_DIR}/Doxyfile @ONLY)
	ADD_CUSTOM_TARGET(Documentation ALL
		${DOXYGEN_EXECUTABLE} ${CMAKE_CURRENT_BINARY_DIR}/Doxyfile
		WORKING_DIRECTORY ${CMAKE_SOURCE_BINARY_DIR}
		VERBATIM
	)
ENDIF()
#END documentation



IF(BUILD_SAMPLES)
#TODO: is there a better way to do this?
#TODO: dedicated output directory!!
	FILE(GLOB_RECURSE CWC_SAMPLE_FIBONACCI_DLL_SOURCES "samples/fibonacci/dll/*.*")
	ADD_LIBRARY(sample-fibonacci-dll SHARED ${CWC_SAMPLE_FIBONACCI_DLL_SOURCES} "samples/fibonacci/cwc.sample.fibonacci.cwch")
	TARGET_LINK_LIBRARIES(sample-fibonacci-dll ${CWC_API_LIB_NAME})
	SET_TARGET_PROPERTIES(sample-fibonacci-dll PROPERTIES
		CXX_STANDARD 11
		CXX_STANDARD_REQUIRED ON
	)

	FILE(GLOB_RECURSE CWC_SAMPLE_FIBONACCI_EXE_SOURCES "samples/fibonacci/exe/*.*")
	ADD_EXECUTABLE(sample-fibonacci ${CWC_SAMPLE_FIBONACCI_EXE_SOURCES} "samples/fibonacci/cwc.sample.fibonacci.cwch")
	TARGET_LINK_LIBRARIES(sample-fibonacci ${CWC_HOST_LIBS})
	TARGET_COMPILE_DEFINITIONS(sample-fibonacci PRIVATE CWC_HOST)
	SET_TARGET_PROPERTIES(sample-fibonacci PROPERTIES
		CXX_STANDARD 11
		CXX_STANDARD_REQUIRED ON
	)

	SET_SOURCE_FILES_PROPERTIES(samples/fibonacci/dll/cwc.sample.fibonacci.cwc PROPERTIES
		LANGUAGE CXX
		COMPILE_FLAGS "-xc++"#NOTE: this only works as VC++ ignores unknown flags; this flag is necessary because otherwise GCC won't compile this file
	)
	SET_SOURCE_FILES_PROPERTIES(samples/fibonacci/cwc.sample.fibonacci.cwch samples/fibonacci/cwc.sample.fibonacci.cwcm PROPERTIES
		HEADER_FILE_ONLY TRUE
	)
ENDIF()